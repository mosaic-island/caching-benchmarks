plugins {
    id 'base'
    id 'com.github.ben-manes.versions'   version '0.15.0'
    id 'com.github.hierynomus.license'   version '0.11.0'
    id 'net.ltgt.apt'                    version '0.11'  apply false
    id 'com.github.johnrengelman.shadow' version '2.0.1'
}

subprojects {
    apply plugin: 'java'
    apply plugin: 'com.github.ben-manes.versions'
    apply plugin: 'com.github.hierynomus.license'
    apply plugin: 'net.ltgt.apt'
    apply plugin: 'com.github.johnrengelman.shadow'

    repositories {
        jcenter()
        mavenCentral()
    }

    dependencies {
        compile "org.slf4j:slf4j-api:$slf4jVersion"
        compile "org.eclipse.persistence:javax.persistence:$jpaVersion"
        compile "org.hamcrest:hamcrest-library:$hamcrestVersion"

        compile "junit:junit:$junitVersion"
        runtime("com.h2database:h2:$h2Version") { transitive = false }
        runtime "org.slf4j:slf4j-simple:$slf4jVersion"
        runtime "org.slf4j:jcl-over-slf4j:$slf4jVersion"
        runtime "org.slf4j:jul-to-slf4j:$slf4jVersion"

        compile     "org.openjdk.jmh:jmh-core:$jmhVersion"
        apt         "org.openjdk.jmh:jmh-generator-annprocess:$jmhVersion"
        compileOnly "org.openjdk.jmh:jmh-generator-annprocess:$jmhVersion"
    }

    license {
        header = rootProject.file('gradle/LICENSE_TEMPLATE')
        strictCheck = true
        ignoreFailures = true
        mapping {
            java = 'SLASHSTAR_STYLE'
            xml  = 'XML_STYLE'
        }
        ext.year    = '2017'
        ext.author  = 'Andres Almiray'
        ext.project = 'JavaTrove Examples'
    }

    test {
        environment = [
            rootDir: rootProject.projectDir.absolutePath
        ]
    }

    jar {
        manifest {
            attributes (
                'Main-Class': 'org.openjdk.jmh.Main'
            )
        }
    }
}
